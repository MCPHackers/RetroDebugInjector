plugins {
	id 'java'
	id 'maven-publish'
}
archivesBaseName = 'rdi'

def ENV = System.getenv()

allprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'

	group 'org.mcphackers.rdi'
    project.version = '1.1'
    if(!ENV.RELEASE.equals('1')) {
        project.version += '-SNAPSHOT'
	}

	repositories {
		mavenCentral()
		maven {
			url "https://maven.fabricmc.net/"
		}
	}

	task sourcesJar(type: Jar) {
		archiveClassifier = 'sources'
		from sourceSets.main.allSource
	}

	dependencies {
		implementation "org.ow2.asm:asm:${project.asm_version}"
		implementation "org.ow2.asm:asm-tree:${project.asm_version}"
		implementation "org.ow2.asm:asm-util:${project.asm_version}"
	}
	
	artifacts {
		archives jar
		archives sourcesJar
	}

	afterEvaluate {
		publishing {
			publications {
				mavenJava(MavenPublication) {
					artifactId = project.archivesBaseName
				
					artifact jar
					artifact sourcesJar
				}
			}

			repositories {
				mavenLocal()
				
				if (ENV.MAVEN_URL) {
					maven {
						url ENV.MAVEN_URL
						if (ENV.MAVEN_USERNAME) {
							credentials {
								username ENV.MAVEN_USERNAME
								password ENV.MAVEN_PASSWORD
							}
						}
					}
				}
			}
		}
	}
}

sourceCompatibility = JavaVersion.VERSION_1_5
targetCompatibility = JavaVersion.VERSION_1_5